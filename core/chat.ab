import { env_var_get, echo_error, echo_warning, echo_info, echo_success, input_prompt, input_confirm } from "std/env"
import { request_llm } from "../utils/llm.ab"
import { get_core_prompt, get_python_tool_prompt } from "./prompt.ab"
import { check_llm_output } from "../utils/security.ab"
import { get_python_info } from "../utils/system.ab"
import { split } from "std/text"

pub fun start_chat_session(initial_prompt: [Text]): Null {
    // Step 1: Load configuration from environment variables
    let base_url = trust env_var_get("FUCKIT_BASE_URL")
    let api_key = trust env_var_get("FUCKIT_API_KEY")
    let model_name = trust env_var_get("FUCKIT_MODEL_NAME")

    if base_url == "" or api_key == "" or model_name == "" {
        echo_error("LLM configuration is missing. Please set FUCKIT_BASE_URL, FUCKIT_API_KEY, and FUCKIT_MODEL_NAME environment variables.")
        exit 1
    }

    // Step 2: Initialize histories and system prompt
    let user_history = [Text]
    let assistant_history = [Text]
    let system_prompt = get_core_prompt()

    // Check for python and enhance prompt if available
    let python_info = get_python_info()
    if len(python_info) > 0 {
        let python_details = split(python_info[0], " ")
        let python_cmd = python_details[0]
        let python_version = python_details[1]
        system_prompt += get_python_tool_prompt(python_cmd, python_version)
    }

    // Step 3: Start the interactive chat loop
    let current_user_prompt = "{initial_prompt}"
    loop {
        user_history += [current_user_prompt]

        echo_info("Assistant is thinking...")
        let assistant_response = request_llm(base_url, model_name, api_key, system_prompt, user_history, assistant_history) failed {
            echo_warning("Failed to get response from LLM. Please check your configuration and network.")
            // Ask user if they want to retry
            if input_confirm("Do you want to retry?", true) {
                // Remove the last failed prompt from history before retrying
                user_history = user_history[0..len(user_history)-1]
                continue
            } else {
                exit 1
            }
        }

        assistant_history += [assistant_response]

        // Step 4: Security check and execution
        let is_safe = check_llm_output(assistant_response)

        let should_execute = false
        if is_safe {
            should_execute = true
            echo_success("The script passed the security check.")
        } else {
            echo assistant_response
            echo_warning("We cannot guarantee the safety of the above script. Please review it carefully.")
            should_execute = input_confirm("Do you want to execute it anyway?", false)
        }
        let scode = 0
        let execution_context = ""
        if should_execute {
            if not is_safe {
                echo_info("Executing in 1 second... Press Ctrl+C to cancel.")
                trust $ sleep 1 $
            }
            scode = 0
            let output = $ set -o pipefail; bash -c "set -e; set -o pipefail;\n{assistant_response}" 2>&1 | tee /dev/tty $ failed {
                scode = status
                if status == 201 {
                    echo_info("Session ended by assistant because he thinks the task is complete.")
                    exit 0
                }
                if status == 202 {
                    echo_info("Execution finished with suggestion to end the session.")
                    if input_confirm("Assistant suggested ending the session. End now?", true) {
                        exit 0
                    }
                }
                if status == 203 {
                    echo_info("Assistant requested to continue without user input.")
                    // Skip user input and continue the loop
                } else {
                    echo_warning("Script execution failed with status: {status}")
                }
            }
            if scode == 0:
                echo_success("Execution finished.")
            if is_safe:
                execution_context = "Your echoed script executed successfully, user has seen the output."
            else:
                execution_context = "Script executed with exit code: {scode}.\nScript Output:\n---\n{output}\n---"
        } else {
            echo_warning("Execution skipped by user.")
            execution_context = "User chose not to execute the script."
        }

        // Step 5: Get next user input or continue automatically
        if scode == 203 {
            current_user_prompt = "{execution_context}\n\nUser's next instruction: [AUTO-CONTINUE]"
            continue
        }

        let next_user_input = ""
        loop {
            next_user_input = input_prompt("You: ")
            if next_user_input != "" {
                break
            }
        }
        if next_user_input == "exit" or next_user_input == "quit" {
            break
        }
        current_user_prompt = "{execution_context}\n\nUser's next instruction: {next_user_input}"
    }
    echo_info("Session ended by user.")
}
