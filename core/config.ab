const URL = "https://dev.fuckit.sh"

import { env_var_get, echo_warning } from "std/env"
import { file_exists, file_read } from "std/fs"
import { split } from "std/text"

fun get_persona_config_dir(): Text {
    let home_dir = trust env_var_get("HOME")
    if home_dir == "" {
        return ""
    }
    let persona = trust env_var_get("FUCKIT_PERSONA")
    if persona == "" {
        // Default to 'fuck' if no persona is set
        persona = "fuck"
    }
    return "{home_dir}/.fuckit/{persona}"
}

pub fun llm_config(): [Text] {
    let config_dir = get_persona_config_dir()
    let base_url = trust env_var_get("FUCKIT_BASE_URL")
    let api_key = trust env_var_get("FUCKIT_API_KEY")
    let model_name = trust env_var_get("FUCKIT_MODEL_NAME")
    // Prioritize local config file if exists
    if config_dir != "" and file_exists("{config_dir}/llm_config.env") {
        for line in lines("{config_dir}/llm_config.env") {
            let parts = split(line, "=")
            if len(parts) == 2 {
                let key = parts[0]
                let value = parts[1]
                if {
                    key == "FUCKIT_BASE_URL" {
                        base_url = value
                    }
                    key == "FUCKIT_API_KEY" {
                        api_key = value
                    }
                    key == "FUCKIT_MODEL_NAME" {
                        model_name = value
                    }
                }
            }
        }
    }
    if base_url == "" {
        echo_warning("No Custom LLM Config Found! Using fuckit.sh Official LLM Endpoint.")
        base_url = URL
        api_key = ""
        model_name = ""
    }
    return [base_url, api_key, model_name]
}

pub fun get_custom_prompt(): Text {
    let config_dir = get_persona_config_dir()
    if config_dir != "" and file_exists("{config_dir}/custom_prompt.md") {
        return trust file_read("{config_dir}/custom_prompt.md")
    }
    return ""
}

pub fun get_memory(): Text {
    let config_dir = get_persona_config_dir()
    if config_dir != "" and file_exists("{config_dir}/memory.md") {
        return trust file_read("{config_dir}/memory.md")
    }
    return ""
}
