import { env_var_get, echo_error, echo_warning, echo_info, echo_success, input_confirm } from "std/env"
import { split } from "std/text"
import { array_contains } from "std/array"
import { dir_exists, dir_create, file_chmod, file_exists } from "std/fs"
import { file_download } from "std/http"
import { compare_versions } from "../utils/utils.ab"

pub fun install(home_dir: Text, name: Text, version: Text, url: Text): Null {
    // First, check if already installed, just check version command directly
    let current_version = $ {name} --version 2> /dev/null $ failed : let current_version = ""

    if current_version == "" :
        echo_info("fuckit.sh is not installed. Installing now...")
    else {
        let comparison = compare_versions(current_version, version) failed : let comparison = -1
        if comparison == -1 :
            echo_warning("A new version ({version}) is available. Your current version is {current_version}. Upgrading...")
        else {
            echo_info("You are already using the latest version ({current_version}).")
            exit 0
        }
    }
    if home_dir == "" :
        echo_error("Cannot determine home directory. Please ensure HOME environment variable is set.")

    // Installation / Update process (they are the same here)
    // Step1: Check if the dir exists
    let bin_dir = home_dir + "/.local/bin"
    let bin_path = bin_dir + "/" + name
    if not dir_exists(bin_dir) {
        echo_info("Directory '{bin_dir}' does not exist. Creating it...")
        dir_create(bin_dir)
    }
    // Step2: Check if bin_dir is in PATH
    let path_env = trust env_var_get("PATH")
    // Step3: Download the script and place it in bin_dir
    echo_info("Downloading 'fuckit.sh' to '{bin_dir}'...(via {url})")
    if not file_download(url, bin_path):
        echo_error("No Avaliable CLI Tools Found, Need curl, wget or aria2c.")
    // Step4: Make it executable
    if not file_chmod(bin_path, "+x"):
        echo_error("Failed to set executable permission on '{bin_path}'.")
    // Step5: Verify installation
    let installed_version = $ {bin_path} --version 2> /dev/null $ failed : let installed_version = ""
    if installed_version == "" {
        echo "Verbose Information:"
        echo "  - Home Directory: {home_dir}"
        echo "  - Binary Path: {bin_path}"
        echo "  - Target Version: {version}"
        echo "  - Current Version: {current_version}"
        echo "  - Installed Version: {installed_version}"
        echo "  - PATH: {path_env}"
        echo_error("Installation failed: Unable to verify installation. (It means executing '{bin_path} --version' did not return any output or just failed.) Please check the above information and try again.")
    } else :
        echo_success("fuckit.sh version {installed_version} installed successfully at '{bin_path}'!")
    // Step6: Warn if bin_dir is not in PATH
    if path_env != "" and not array_contains(split(path_env, ":"), bin_dir) {
        echo_warning("Warning: '{bin_dir}' is not in your PATH environment variable. You may need to add it manually to run 'fuckit.sh' easily.")
        echo_info("You can add the following line to your shell profile (e.g., ~/.bashrc, ~/.zshrc):")
        echo_info("export PATH=$PATH:{bin_dir}")
    }
}

pub fun uninstall(home_dir: Text, name: Text): Null {
    if home_dir == "" {
        echo_error("Cannot determine home directory. Please ensure HOME environment variable is set.")
        exit 1
    }

    let bin_dir = home_dir + "/.local/bin"
    let bin_path = bin_dir + "/" + name
    let config_dir = home_dir + "/.fuckit"

    if not file_exists(bin_path) {
        echo_warning("fuckit.sh is not installed at '{bin_path}', nothing to do.")
        exit 0
    }

    echo_info("Starting uninstallation process...")

    // Find and remove all symbolic links (personas/aliases)
    echo_info("Searching for aliases in '{bin_dir}'...")
    let aliases = trust $ find "{bin_dir}" -type l -lname "*/{name}" $
    if aliases != "" {
        for alias_path in lines(aliases) {
            echo_info("  - Removing alias: {alias_path}")
            $ rm "{alias_path}" $ failed {
                echo_warning("    Could not remove alias '{alias_path}'. Please check permissions.")
            }
        }
    } else {
        echo_info("No aliases found.")
    }

    // Remove the main executable
    echo_info("Removing main executable: {bin_path}")
    $ rm "{bin_path}" $ failed {
        echo_error("Failed to remove '{bin_path}'. Please check permissions and try again.")
        exit 1
    }

    // Ask to remove the configuration directory
    if dir_exists(config_dir) {
        if input_confirm("Do you want to remove all configurations and data from '{config_dir}'?", false) {
            echo_info("Removing configuration directory: {config_dir}")
            $ rm -r "{config_dir}" $ failed {
                echo_error("Failed to remove '{config_dir}'. Please check permissions.")
                exit 1
            }
        } else {
            echo_info("Configuration directory '{config_dir}' was not removed.")
        }
    }

    echo_success("fuckit.sh has been successfully uninstalled.")
}
