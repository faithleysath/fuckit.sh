const VERSION = "0.1.1"
const LANG = "en"
const NAME = "fuck"
// const URL = "https://fuckit.sh"
const URL = "http://127.0.0.1:5500/main.sh"

// Check for dependencies first, need _ to avoid stray semicolon. If you can't understand, just keep it as is. It's not a bug, just to fit the compiler's behavior.
let _ = trust $
if ! command -v bc > /dev/null 2>&1; then
    echo -e "\\033[0;31mDependency check failed: 'bc' command not found. Please install 'bc' and try again.\\033[0m" >&2
    echo "  - Debian/Ubuntu: sudo apt-get install bc" >&2
    echo "  - RedHat/CentOS: sudo yum install bc" >&2
    echo "  - Fedora:        sudo dnf install bc" >&2
    echo "  - Arch Linux:    sudo pacman -S bc" >&2
    echo "  - openSUSE:      sudo zypper install bc" >&2
    echo "  - Alpine:        apk add bc" >&2
    exit 1
fi
$

import { env_var_get, echo_error, echo_warning, echo_info, echo_success } from "std/env"
import { starts_with, split, parse_number } from "std/text"
import { array_contains } from "std/array"
import { dir_exists, dir_create, file_chmod } from "std/fs"
import { file_download } from "std/http"

fun compare_versions(v1: Text, v2: Text): Num? {
    let parts1 = split(v1, ".")
    let parts2 = split(v2, ".")

    let len1 = len(parts1)
    let len2 = len(parts2)

    let max_len = len1 > len2 then len1 else len2

    for i in 0..max_len {
        // If part is missing, treat it as 0
        let num1 = i < len1 then parse_number(parts1[i])? else 0
        let num2 = i < len2 then parse_number(parts2[i])? else 0

        if num1 > num2 : return 1
        if num1 < num2 : return -1
    }

    return 0
}

fun install(home_dir: Text): Null {
    // First, check if already installed, just check version command directly
    let current_version = $ {NAME} --version 2> /dev/null $ failed : let current_version = ""

    if current_version == "" :
        echo_info("fuckit.sh is not installed. Installing now...")
    else {
        let comparison = compare_versions(current_version, VERSION) failed : let comparison = -1
        if comparison == -1 :
            echo_warning("A new version ({VERSION}) is available. Your current version is {current_version}. Updating...")
        else {
            echo_info("You are already using the latest version ({current_version}).")
            exit 0
        }
    }

    // Installation / Update process (they are the same here)
    // Step1: Check if the dir exists
    let bin_dir = home_dir + "/.local/bin"
    let bin_path = bin_dir + "/" + NAME
    if not dir_exists(bin_dir) {
        echo_info("Directory '{bin_dir}' does not exist. Creating it...")
        dir_create(bin_dir)
    }
    // Step2: Check if bin_dir is in PATH
    let path_env = trust env_var_get("PATH")
    // Step3: Download the script and place it in bin_dir
    echo_info("Downloading 'fuckit.sh' to '{bin_dir}'...(via {URL})")
    if not file_download(URL, bin_path) :
        echo_error("No Avaliable CLI Tools Found, Need curl, wget or aria2c.")
    // Step4: Make it executable
    if not file_chmod(bin_path, "+x") :
        echo_error("Failed to set executable permission on '{bin_path}'.")
    // Step5: Verify installation
    let installed_version = $ {bin_path} --version 2> /dev/null $ failed : let installed_version = ""
    if installed_version == "" {
        echo "Verbose Information:"
        echo "  - Home Directory: {home_dir}"
        echo "  - Binary Path: {bin_path}"
        echo "  - Target Version: {VERSION}"
        echo "  - Current Version: {current_version}"
        echo "  - Installed Version: {installed_version}"
        echo "  - PATH: {path_env}"
        echo_error("Installation failed: Unable to verify installation. (It means executing '{bin_path} --version' did not return any output or just failed.) Please check the above information and try again.")
    } else :
        echo_success("fuckit.sh version {installed_version} installed successfully at '{bin_path}'!")
    // Step6: Warn if bin_dir is not in PATH
    if path_env != "" and not array_contains(split(path_env, ":"), bin_dir) {
        echo_warning("Warning: '{bin_dir}' is not in your PATH environment variable. You may need to add it manually to run 'fuckit.sh' easily.")
        echo_info("You can add the following line to your shell profile (e.g., ~/.bashrc, ~/.zshrc):")
        echo_info("export PATH=\"{bin_dir}:\$PATH\"")
    }
}

main (arguments) {
    // Handle zero arguments case (although this should not happen)
    if len(arguments) < 1 :
        echo_error("Oops, no arguments provided! It should not happen. But it did. Please visit https://fuckit.sh and report this issue.")
    // First argument is the script name
    let script_name = arguments[0]
    // Remaining arguments are passed to the script
    let script_args = arguments[1..len(arguments)]
    // Get the home directory from environment variable
    let home_dir = trust env_var_get("HOME")

    // Check execution mode
    if {
        // Print Version (Highest Priority)
        len(script_args) == 1 and array_contains(["-v", "--version", "version"], script_args[0]) :
            echo VERSION

        // Guard for HOME variable during install attempt
        len(script_args) == 0 and home_dir == "" :
            echo_error("Install failed: HOME environment variable is not set. Try 'export HOME=~' and run again.")

        // Installation Mode
        len(script_args) == 0 and not starts_with(script_name, home_dir + "/.local/bin") : 
            install(home_dir)

        // Default case: Normal execution
        else {
            echo_info("Entering Normal Execution Mode...")
            echo "Usage: {NAME} [options]"
        }
    }
}
