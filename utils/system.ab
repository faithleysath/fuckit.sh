import { is_command, env_var_get } from "std/env"
import { file_exists, file_read } from "std/fs"
import { array_contains } from "std/array"
import { split, replace } from "std/text"
import { compare_versions } from "./utils.ab"

pub fun get_package_managers(): [Text] {
    let managers = ["apt", "yum", "dnf", "zypper", "pacman", "emerge", "apk", "brew", "pkg", "nix-env", "guix", "snap", "flatpak"]
    let available_managers = [Text]

    for manager in managers {
        if is_command(manager) {
            available_managers += [manager]
        }
    }
    return available_managers
}

pub fun get_shell_profiles(): [Text] {
    let home_dir_from_env = trust env_var_get("HOME")
    let home_dir = home_dir_from_env == "" then ($ echo ~ $ failed: "") else home_dir_from_env

    if home_dir == "" {
        return [Text]
    }

    let profiles = ["/.bashrc", "/.zshrc", "/.profile", "/.bash_profile", "/.config/fish/config.fish"]
    let available_profiles = [Text]

    for profile in profiles {
        let full_path = home_dir + profile
        if file_exists(full_path) {
            available_profiles += [full_path]
        }
    }
    return available_profiles
}

pub fun get_os_info(): Text {
    let info = ""
    if is_command("sw_vers"):
        info += (trust $ sw_vers 2>/dev/null $) + "\n"

    if file_exists("/etc/os-release"):
        info += (trust $ cat /etc/os-release 2>/dev/null $) + "\n"

    info += trust $ uname -a 2>/dev/null $
    return info
}

pub fun get_current_path(): Text {
    return trust $ pwd $
}

pub fun get_python_info(): [Text] {
    let python_commands = ["python3", "python", "py", "python2"]
    let available_pythons = [Text]

    for cmd in python_commands {
        if is_command(cmd) {
            let version_output = trust $ {cmd} --version 2>&1 $
            let version = trust $ echo "{version_output}" | tr -d '\n\r' | sed 's/^.* //' $
            available_pythons += ["{cmd} {version}"]
        }
    }
    return available_pythons
}

pub fun execute_python3_script(script: Text, args: [Text] = [Text]): Text? {
    let available_pythons = get_python_info()
    if len(available_pythons) == 0 : fail 1
    let python_cmd_version = split(available_pythons[0], " ")
    let python_cmd = python_cmd_version[0]
    let python_version = python_cmd_version[1]
    // Check if Python version is at least 3.6
    if compare_versions(python_version, "3.6")? < 0 : fail 1
    let escaped_script = replace(script, "'", "'\\''")
    let full_command = "{python_cmd} -c '{escaped_script}'"
    for arg in args {
        // Safely quote each argument
        let quoted_arg = $ printf "%q" "{arg}" $?
        full_command += " {quoted_arg}"
    }
    return $ sh -c "{full_command}" $?
}
